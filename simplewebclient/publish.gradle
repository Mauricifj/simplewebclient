apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka'

// Create the pom configuration:
def pomConfig = {
    developers {
        developer {
            id developerId
            name developerName
            email developerEmail
        }
    }

    scm {
        url developerUrl
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    dependsOn (dokka)
    archiveClassifier = 'javadoc'
    from "$buildDir/docs/javadoc"
}

dokka {
    outputFormat = 'html-as-java'
    outputDirectory = "$buildDir/docs/javadoc"
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release

                artifact sourcesJar
                artifact javadocJar

                groupId publishedGroupId
                artifactId artifact
                version libraryVersion

                // Define this explicitly if using implementation or api configurations
                pom.withXml {
                    def root = asNode()
                    root.appendNode('description', libraryDescription)
                    root.appendNode('name', libraryName)
                    root.children().last() + pomConfig

                    // Define this explicitly if using implementation or api configurations
                    def dependenciesNode = asNode().getAt('dependencies')[0] ?: asNode().appendNode('dependencies')
                    // Iterate over the implementation dependencies (we don't want the test ones), adding a <dependency> node for each
                    configurations.api.allDependencies.each {
                        // Ensure dependencies such as fileTree are not included.
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
    }
}

Properties properties = new Properties()
if (project.rootProject.file('local.properties').canRead()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
    ext.bintray_user = properties.getProperty('BINTRAY_USER')
    ext.bintray_key = properties.getProperty('BINTRAY_KEY')
} else {
    ext.bintray_user = System.getenv('BINTRAY_USER')
    ext.bintray_key = System.getenv('BINTRAY_KEY')
}

bintray {
    user = bintray_user
    key = bintray_key
    publications = ['release']
    override = true
    pkg {
        repo = bintrayRepo
        name = bintrayName
        licenses = allLicenses
        publish = true
        vcsUrl = gitUrl
        publicDownloadNumbers = true
        version {
            name = libraryVersion
            desc = libraryDescription
            released = new Date()
            vcsTag = libraryVersion
        }
    }
}